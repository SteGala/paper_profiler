#--------------------------------------
# Profiling 
# -------------------------------------
set style line 1 lc rgb 'red' pt 5 lw 3  # square
set style line 2 lc rgb 'green' pt 7 lw 3  # circle
set style line 3 lc rgb 'blue' pt 9 lw 3  # triangle
set style line 4 lc rgb 'black' pt 11 lw 3
set style line 5 lc rgb 'blue' pt 8 lw 3# triangle empty
set style line 6 lc rgb 'orange' pt 10 lw 3# triangle empty

set xrange [0:1600]
set term postscript eps color size 3.5,2.5 font 22
set ylabel "CPU (cores)"
set xlabel "Time (seconds)"
set yrange [0:3]
set xtics 400
set grid ytics
set output "./img/Profiling_CPU.ps"

plot 'Profiling_CPU' using 1:3 with linespoints title 'RAYGO' ls 1, \
'' using 1:4 with linespoints title 'usage' ls 2

set ylabel "RAM (MB)"
set yrange [0:1000]
set output "./img/Profiling_RAM.ps"

plot 'Profiling_RAM' using 1:3 with linespoints title 'RAYGO' ls 1, \
'' using 1:4 with linespoints title 'usage' ls 2


set arrow from 16, graph 0 to 16, graph 1 nohead lt 0 lw 2
set arrow from 32, graph 0 to 32, graph 1 nohead lt 0 lw 2
set arrow from 49, graph 0 to 49, graph 1 nohead lt 0 lw 2
set term postscript eps color size 7,2.5 font 22


# -------------------------------------
# set parameters common to every graph
# -------------------------------------
set boxwidth 2
set xrange [0:66]
set y2range [0:2500]
set y2tics 0, 500
set ytics nomirror
set y2label "Users"
set xlabel "Time"
unset xtics


# -------------------------------------
# Success Rate
# -------------------------------------

set yrange [90:100.5]
set ylabel "Success Rate (%)"
set key above
set grid ytics
set output "./img/Success rate.ps"

plot 'Success rate' using ($1-3):2 title 'VPA' w p ls 1 ps 2, \
'' using ($1-1):3 title 'HPA' w p ls 2 ps 2, \
'' using ($1+1):5 title ' RAYGO (HDP)' w p ls 5 ps 2, \
'' using ($1+3):6 title ' RAYGO (HDP+EDP single)' w p ls 4 ps 2, \
'' using ($1+5):4 title ' RAYGO (HDP+EDP app)' w p ls 3 ps 2, \
'Users' using 1:2 axis x1y2 with lines title 'workload' lw 1 lt rgb "blue"


# -------------------------------------
# RPS plot
# -------------------------------------
set yrange [200:450]
set ylabel "Successful responses"
set output "./img/Requests per second.ps"

plot 'Requests per second' using ($1-4):25 title 'UNCONSTRAINED' w p ls 4 ps 2, \
'' using ($1-2):7 title 'VPA' w p ls 1 ps 2, \
'' using ($1):13 title 'HPA' w p ls 2 ps 2, \
'' using ($1+2):31 title ' RAYGO (HDP)' w p ls 5 ps 2, \
'' using ($1+4):37 title ' RAYGO (HDP+EDP single)' w p ls 6 ps 2, \
'' using ($1+6):19 title ' RAYGO (HDP+EDP app)' w p ls 3 ps 2, \
'Users' using 1:2 axis x1y2 with lines title 'workload' lw 1 lt rgb "blue"

# -------------------------------------
# CPU limits plot
# -------------------------------------

set yrange [0:13]
set ylabel "CPU cores"
set output "./img/CPU limits.ps"

# Candlestick data columns: X BoxMax Min Max BoxMax
plot 'CPU Limits' using ($1-3):7 title 'VPA' with boxes fill pattern 6 lt rgb "green", \
'' using ($1-3):7:2:6:7 notitle with candlesticks lt -1 lw 2, \
'' using ($1-1):13 title 'HPA' with boxes fill pattern 4 lt rgb "blue", \
'' using ($1-1):13:8:12:13 notitle with candlesticks lt -1 lw 2, \
'' using ($1+1):25 title ' RAYGO (HDP)' with boxes fill pattern 9 lt rgb "orange", \
'' using ($1+1):25:20:24:25 notitle with candlesticks lt -1 lw 2, \
'' using ($1+3):31 title ' RAYGO (HDP+EDP single)' with boxes fill pattern 10 lt rgb "black", \
'' using ($1+3):31:26:30:31 notitle with candlesticks lt -1 lw 2, \
'' using ($1+5):19 title ' RAYGO (HDP+EDP app)' with boxes fill pattern 5 lt rgb "red", \
'' using ($1+5):19:14:18:19 notitle with candlesticks lt -1 lw 2, \
'Users' using 1:2 axis x1y2 with lines title 'workload' lw 1 lt rgb "blue"


# -------------------------------------
# CPU requests plot
# -------------------------------------

set yrange [0:6.5]
set ylabel "CPU cores"
set output "./img/CPU requests.ps"

plot 'CPU request' using ($1-3):7 title 'VPA' with boxes fill pattern 6 lt rgb "green", \
'' using ($1-3):7:2:6:7 notitle with candlesticks lt -1 lw 2, \
'' using ($1-1):13 title 'HPA' with boxes fill pattern 4 lt rgb "blue", \
'' using ($1-1):13:8:12:13 notitle with candlesticks lt -1 lw 2, \
'' using ($1+1):25 title ' RAYGO (HDP)' with boxes fill pattern 9 lt rgb "orange", \
'' using ($1+1):25:20:24:25 notitle with candlesticks lt -1 lw 2, \
'' using ($1+3):31 title ' RAYGO (HDP+EDP single)' with boxes fill pattern 10 lt rgb "black", \
'' using ($1+3):31:26:30:31 notitle with candlesticks lt -1 lw 2, \
'' using ($1+5):19 title ' RAYGO (HDP+EDP app)' with boxes fill pattern 5 lt rgb "red", \
'' using ($1+5):19:14:18:19 notitle with candlesticks lt -1 lw 2, \
'Users' using 1:2 axis x1y2 with lines title 'workload' lw 1 lt rgb "blue"


# -------------------------------------
# Request per core on Requests plot
# -------------------------------------

set yrange [0:200]
set ylabel "Requests per core"
set output "./img/Requests per core on Requests.ps"

plot 'Request per core on Requests' using ($1-3):7 title 'VPA' with boxes fill pattern 6 lt rgb "green", \
'' using ($1-3):7:2:6:7 notitle with candlesticks lt -1 lw 2, \
'' using ($1-1):13 title 'HPA' with boxes fill pattern 4 lt rgb "blue", \
'' using ($1-1):13:8:12:13 notitle with candlesticks lt -1 lw 2, \
'' using ($1+1):25 title ' RAYGO (HDP)' with boxes fill pattern 9 lt rgb "orange", \
'' using ($1+1):25:20:24:25 notitle with candlesticks lt -1 lw 2, \
'' using ($1+3):31 title ' RAYGO (HDP+EDP single)' with boxes fill pattern 10 lt rgb "black", \
'' using ($1+3):31:26:30:31 notitle with candlesticks lt -1 lw 2, \
'' using ($1+5):19 title ' RAYGO (HDP+EDP app)' with boxes fill pattern 5 lt rgb "red", \
'' using ($1+5):19:14:18:19 notitle with candlesticks lt -1 lw 2, \
'Users' using 1:2 axis x1y2 with lines title 'workload' lw 1 lt rgb "blue"


# -------------------------------------
# Request per core on Limits plot
# -------------------------------------

set yrange [0:90]
set ylabel "Requests per core"
set output "./img/Requests per core on Limits.ps"

plot 'Request per core on Limits' using ($1-3):7 title 'VPA' with boxes fill pattern 6 lt rgb "green", \
'' using ($1-3):7:2:6:7 notitle with candlesticks lt -1 lw 2, \
'' using ($1-1):13 title 'HPA' with boxes fill pattern 4 lt rgb "blue", \
'' using ($1-1):13:8:12:13 notitle with candlesticks lt -1 lw 2, \
'' using ($1+1):25 title ' RAYGO (HDP)' with boxes fill pattern 9 lt rgb "orange", \
'' using ($1+1):25:20:24:25 notitle with candlesticks lt -1 lw 2, \
'' using ($1+3):19 title ' RAYGO (HDP+EDP)' with boxes fill pattern 5 lt rgb "red", \
'' using ($1+3):19:14:18:19 notitle with candlesticks lt -1 lw 2, \
'Users' using 1:2 axis x1y2 with lines title 'workload' lw 1 lt rgb "blue"


# -------------------------------------
# Response time P95 boxplot
# -------------------------------------

set yrange [10:3000]
set ylabel "Response time"
set boxwidth 1.9
set logscale y 10
set output "./img/Response time P95 boxplot.ps"

plot 'Response time p95 boxplot' using ($1-3):3:2:6:5 with candlesticks title 'VPA' whiskerbars fill pattern 2 lt rgb "violet", \
'' using ($1-3):4:4:4:4 notitle with candlesticks lt -1 lw 2, \
'' using ($1-1):8:7:11:10 with candlesticks title 'HPA' whiskerbars fill pattern 4 lt rgb "blue", \
'' using ($1-1):9:9:9:9 notitle with candlesticks lt -1  lw 2, \
'' using ($1+1):18:17:21:20 with candlesticks title ' RAYGO (HDP)' whiskerbars fill pattern 9 lt rgb "orange", \
'' using ($1+1):19:19:19:19 notitle with candlesticks lt -1  lw 2, \
'' using ($1+3):23:22:26:25 with candlesticks title ' RAYGO (HDP+EDP single)' whiskerbars fill pattern 10 lt rgb "black", \
'' using ($1+3):24:24:24:24 notitle with candlesticks lt -1  lw 2, \
'' using ($1+5):13:12:16:15 with candlesticks title ' RAYGO (HDP+EDP app)' whiskerbars fill pattern 5 lt rgb "red", \
'' using ($1+5):14:14:14:14 notitle with candlesticks lt -1  lw 2, \
'Users' using 1:2 axis x1y2 with lines title 'workload' lw 1 lt rgb "blue"
